###########################################################
# Environment and Language Settings                       #
###########################################################

# Language settings
export LANG="en_US.UTF-8"
export LC_ALL="en_US.UTF-8"
export LC_CTYPE="en_US.UTF-8"

# Use XDG_CONFIG_HOME if set, otherwise default to ${HOME}/.config
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export XDG_DATA_HOME="${XDG_DATA_HOME:-$HOME/.local/share}"
export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"

###########################################################
# Utility Functions                                       #
###########################################################

# Function to safely append a directory to PATH if it's not already included
add_to_path() {
  if [[ ":$PATH:" != *":$1:"* ]]; then
    PATH="$1:${PATH}"
  fi
}

# Function to check if a command exists
command_exists() {
  command -v "$1" &>/dev/null
}

# Function to load tools if they exist
load_if_exists() {
  if command_exists "$1"; then
    eval "$2"
  fi
}

# Function to create a directory if it doesn't exist
ensure_dir_exists() {
  if [[ ! -d "$1" ]]; then
    mkdir -p "$1"
  fi
}

###########################################################
# History Configuration                                   #
###########################################################

HISTDIR="${XDG_DATA_HOME}/zsh"
HISTFILE="${HISTDIR}/history"

# Ensure history directory exists
ensure_dir_exists "$HISTDIR"

HISTSIZE=10000
SAVEHIST=10000
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_VERIFY

###########################################################
# Command-Line Enhancements                               #
###########################################################

# Load awesome shell tools only if they're installed

# Mise
load_if_exists mise 'eval "$(mise activate zsh)"'

# lsd (ls replacement)
load_if_exists lsd 'alias ls="lsd" && alias l="lsd -l"'

# bat (cat replacement)
if command_exists bat; then
  alias cat='bat'
  export MANPAGER="sh -c 'col -bx | bat -l man -p'"
  alias -g -- -h='-h 2>&1 | bat --language=help --style=plain'
  alias -g -- --help='--help 2>&1 | bat --language=help --style=plain'
fi

# fzf (fuzzy finder)
load_if_exists fzf 'eval "$(fzf --zsh)"'

# zoxide (cd replacement)
load_if_exists zoxide 'eval "$(zoxide init zsh)"'

# starship prompt
load_if_exists starship 'eval "$(starship init zsh)"'

# broot (terminal file manager)
if [[ -f "${XDG_CONFIG_HOME}/broot/launcher/bash/br" ]]; then
  source "${XDG_CONFIG_HOME}/broot/launcher/bash/br"
fi

# ngrok completion
load_if_exists ngrok 'eval "$(ngrok completion)"'

###########################################################
# Homebrew Setup                                          #
###########################################################
if command_exists brew; then
  # Store Homebrew prefix
  BREW_PREFIX=$(brew --prefix)

  # Add Homebrew's zsh completions to FPATH
  FPATH="${BREW_PREFIX}/share/zsh/site-functions:${FPATH}"
  
  # Load completions
  autoload -Uz compinit
  compinit

  # Homebrew settings
  export HOMEBREW_NO_ANALYTICS=1
  export HOMEBREW_NO_ENV_HINTS=1

  # Google Cloud SDK (if installed via Homebrew)
  GCP_SDK_PATH="${BREW_PREFIX}/share/google-cloud-sdk"
  if [[ -d "$GCP_SDK_PATH" ]]; then
    source "${GCP_SDK_PATH}/path.zsh.inc"
    source "${GCP_SDK_PATH}/completion.zsh.inc"
  fi
fi


###########################################################
# Key Bindings                                            #
###########################################################

# Custom key overrides
bindkey \^U backward-kill-line


###########################################################
# Final PATH Adjustments                                  #
###########################################################

# Add local bin directories to PATH
add_to_path "$HOME/.local/bin"
add_to_path "$HOME/bin"

# Remove duplicate PATH entries
typeset -U PATH