###########################################################
# Environment and Language Settings                       #
###########################################################

# Language settings
export LANG="en_US.UTF-8"
export LC_ALL="en_US.UTF-8"
export LC_CTYPE="en_US.UTF-8"

# Use XDG_CONFIG_HOME if set, otherwise default to ${HOME}/.config
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export XDG_DATA_HOME="${XDG_DATA_HOME:-$HOME/.local/share}"
export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"


###########################################################
# Utility Functions                                       #
###########################################################

# Function to safely append a directory to PATH if it's not already included
add_to_path() {
  if [[ ":$PATH:" != *":$1:"* ]]; then
    PATH="$1:${PATH}"
  fi
}

# Function to check if a command exists
command_exists() {
  command -v "$1" &>/dev/null
}

# Function to load tools if they exist
load_if_exists() {
  if command_exists "$1"; then
    eval "$2"
  fi
}

# Function to create a directory if it doesn't exist
ensure_dir_exists() {
  if [[ ! -d "$1" ]]; then
    mkdir -p "$1"
  fi
}

###########################################################
# Homebrew Setup                                          #
###########################################################
if command_exists brew; then
  # Store Homebrew prefix
  local BREW_PREFIX=$(brew --prefix)

  # Add Homebrew's zsh completions to FPATH
  FPATH="${BREW_PREFIX}/share/zsh/site-functions:${FPATH}"

  # Homebrew settings
  export HOMEBREW_NO_ANALYTICS=1
  export HOMEBREW_NO_ENV_HINTS=1
fi


###########################################################
# History Configuration                                   #
###########################################################

HISTDIR="${XDG_DATA_HOME}/zsh"
HISTFILE="${HISTDIR}/history"

# Ensure history directory exists
ensure_dir_exists "$HISTDIR"

HISTSIZE=10000
SAVEHIST=10000
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_VERIFY

###########################################################
# Command-Line Enhancements                               #
###########################################################

# Load awesome shell tools only if they're installed

# Mise
load_if_exists mise 'eval "$(mise activate zsh)"'

# lsd (ls replacement)
load_if_exists lsd 'alias ls="lsd" && alias l="lsd -l"'

# fzf (fuzzy finder)
load_if_exists fzf 'eval "$(fzf --zsh)"'

# zoxide (cd replacement)
load_if_exists zoxide 'eval "$(zoxide init zsh)"'

# starship prompt
load_if_exists starship 'eval "$(starship init zsh)"'

# broot (terminal file manager)
if [[ -f "${XDG_CONFIG_HOME}/broot/launcher/bash/br" ]]; then
  source "${XDG_CONFIG_HOME}/broot/launcher/bash/br"
fi

# GCP path setup (completion loaded later)
if command_exists brew; then
  local GCP_SDK_PATH="$(brew --prefix)/share/google-cloud-sdk"
  if [[ -d "$GCP_SDK_PATH" ]]; then
    source "${GCP_SDK_PATH}/path.zsh.inc"
  fi
fi


###########################################################
# Key Bindings                                            #
###########################################################

# Custom key overrides
bindkey \^U backward-kill-line

# Make sure ESC+backspace treats common separators as word separators
WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'

###########################################################
# Final PATH Adjustments                                  #
###########################################################

# Add local bin directories to PATH
add_to_path "$HOME/.local/bin"
add_to_path "$HOME/bin"

# Add Antigravity to PATH
add_to_path "$HOME/.antigravity/antigravity/bin"

# Remove duplicate PATH entries
typeset -U PATH

###########################################################
# Common Aliases                                          #
###########################################################

# Git aliases
alias ga='git add'
alias gc='git commit -m'
alias gps='git push'
alias gpl='git pull'
alias gs='git status'
alias gco='git checkout'
alias gb='git branch'

# Git add all and commit
gac() {
    git add .
    git commit -m "$1"
}

# Create a new branch from main
gnb() {
    git checkout main
    git pull
    git checkout -b "$1"
}

# Directory navigation
alias ..='cd ..'
alias ...='cd ../..'

# Modal commands
alias mr='modal run'
alias md='modal deploy'

# Python and package management
alias uvpi='uv pip install'
alias uvpc='uv pip compile'
alias uvlu='uv lock --upgrade'

# Misc
alias c='code .'
alias z='vi ${HOME}/.zshrc'
alias bubu='brew update && brew upgrade'

# Chezmoi
alias cma='chezmoi add'
alias cmc='chezmoi cd'

# Repos access
rep() {
    if [ $# -eq 0 ]; then
        cd ~/repos
    else
        cd ~/repos/$1
    fi
}

###########################################################
# Completion Setup                                        #
###########################################################

# Add custom completion directory
fpath+=~/.zfunc

# Docker completions
if [[ -d "$HOME/.docker/completions" ]]; then
  fpath=("$HOME/.docker/completions" $fpath)
fi

# Load completions (with caching for speed)
autoload -Uz compinit
# Only regenerate compinit cache once per day
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi

# Completion styling
zstyle ':completion:*' menu select

# Tool completions (loaded after compinit)
load_if_exists ngrok 'eval "$(ngrok completion)"'
eval "$(uvx --generate-shell-completion zsh)"
eval "$(uv generate-shell-completion zsh)"

# GCP completions
if command_exists brew; then
  local GCP_SDK_PATH="$(brew --prefix)/share/google-cloud-sdk"
  if [[ -d "$GCP_SDK_PATH" ]]; then
    source "${GCP_SDK_PATH}/completion.zsh.inc"
  fi
fi
